pkgname=st-custom-git
_pkgname=st
pkgver=883.d032b61
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft')
makedepends=('libxext' 'git' 'ncurses' 'freetype2' 'xorg-appres')
source=("$_pkgname::git://git.suckless.org/st" 'config.sh' 'colors.patch')
sha1sums=('SKIP' 'SKIP' 'SKIP')

provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")

pkgver() {
	cd "$srcdir/$_pkgname"
	echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
	cd "$srcdir/$_pkgname"
	sed -i config.mk \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g'

	rm -f config.h

	# pull configuration from Xresources (requires xorg-appres)
	# this includes colors, font, and some minor fixes
	../config.sh

	# patch to support 256+ configurable colors
	git apply --ignore-space-change --ignore-whitespace ../colors.patch

	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "$srcdir/$_pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
