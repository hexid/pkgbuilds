pkgbase=sys-dotfiles
pkgname=(sys-dotfiles sys-dotfiles-xorg)
pkgver=0.1.2
pkgrel=1
pkgdesc='system dotfiles'
arch=('any')
url='https://github.com/hexid/pkgbuilds'

source=(
	'etc_locale.conf'
	'etc_vconsole.conf'
	'modprobe_pcspkr.conf'
	'pacman_check-boot.hook'
	'pacman_sync.hook'
	'zsh_zshenv'
)

md5sums=('e24819fd5acc025b5b307b77bb871f65'
         '363d1b98421f052a095af688c2b4fc5a'
         '3866086e6af5e3528a66eff492f2f4dd'
         '96d59a4bb769688f3271c5d2f0e36892'
         'f01801e1fba8be42c9ac9a64c62eebaa'
         '19e9253e0d19cb09242142398d92f160')

package_sys-dotfiles() {
	pkgdesc='common system dotfiles'
	depends=(
		'arch-audit'
		'git'
		'htop'
		'kernel-modules-hook'
		'neovim'
		'openssh'
		'sudo'
		'systemd-boot-pacman-hook'
		'tmux'
		'zsh'
	)
	optdepends=(
		'direnv'
		'hwinfo'
		'moreutils'
		'syncthing'
		'wireguard-tools'
	)

	install -Dm0644 etc_locale.conf "${pkgdir}"/etc/locale.conf
	install -Dm0644 etc_vconsole.conf "${pkgdir}"/etc/vconsole.conf

	install -Dm0644 modprobe_pcspkr.conf "${pkgdir}"/etc/modprobe.d/pcspkr.conf

	install -Dm0644 pacman_check-boot.hook "${pkgdir}"/etc/pacman.d/hooks/00-check-boot.hook
	install -Dm0644 pacman_sync.hook "${pkgdir}"/etc/pacman.d/hooks/sync.hook

	install -Dm0644 zsh_zshenv "${pkgdir}"/etc/zsh/zshenv

	ln -sf /usr/share/zoneinfo/America/New_York "${pkgdir}"/etc/localtime
}

package_sys-dotfiles-xorg() {
	pkgdesc='xorg system dotfiles'
	depends=(
		'sys-dotfiles'

		'arandr'
		'dmenu'
		'dunst'
		'feh'
		'flameshot'
		'herbstluftwm'
		'otf-font-awesome'
		'pavucontrol'
		'sxhkd'
		'ttf-dejavu'
		'xorg-server'
		'xorg-xinit'

		'slock-custom-git'
		'st-custom-git'

		'equilux-theme'
		'firefox-developer-edition'
		'polybar'
	)
	optdepends=(
		'baobab: visual disk usage'
		'code: text editor'
		'openrgb: rgb light configuration'
		'picard: music tag editor'
		'piper: mouse configuration'
		'transmission-remote-gtk: torrent monitor'
	)
}
